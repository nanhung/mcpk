
# Chemical Parameters
## Molecular Weights
MWTCE = 131.39;  # TCE

# Global Model Parameters 
QC	= 1;	# Cardiac output (L/hr)
VPR	= 1;	# Alveolar ventilation-perfusion ratio
QPsamp	= 1;	# Alveolar ventilation (L/hr)
DResptmp	= 1;	# Respiratory lumen:tissue diffusive clearance rate (L/hr) [scaled to QP]
KMClara	= 1;	# KM for Tracheo-bronchial TCE oxidation (mg/L)

## Volumes
VRespEff	= 1;
VRespLum	= 1;	# Volume of respiratory lumen (L air)

## Potentially measured covariates (constants)
PBmeas	= 0.0;	# Measured blood-air partition coefficient

## Flag for species, sex -- these are global parameters
BW	= 0.0;  # Species-specific defaults during initialization
Male 	= 1.0;	# 1 = male, 0 = female
Species	= 1.0;	# 1 = human, 2 = rat, 3 = mouse

## Closed chamber nd other exposure parameters 
Rodents = 1;	# Number of rodents in closed chamber data
VCh	= 1;	# Chamber volume for closed chamber data
kLoss	= 1;	# Rate constant for closed chamber air loss
CC 	= 0.0;	# Initial chamber concentration (ppm)
NRodents	= 1;	# 
VChC	= 1;	# 

# Global Sampling Parameters 
VRespLumC	= 1.0;	#(v1.2) Scaled to species-specific central estimates
VMaxClara	= 1.0;
VMax	    = 1.0;
VRespEffC	= 1.0;	

## Fractional Tissue Volumes (fraction of BW)
VLivC	= 1.0;	# Scaled to species-specific central estimates

lnPBC = 0.0;
lnVMaxC	= 0.0;	# Scaled by liver weight and species-specific central estimates
lnVPRC	= 0.0;	# Scaled to species-specific central estimates
lnPRespC =0.0;
lnISOx = 0.0; # P450 oxidative metabolism
lnkLossC	= 0;	# 
lnVMaxLungLivC	= 0.0;	#(v1.2) Ratio of lung Vmax to liver Vmax,
lnKMClara	= 0.0;	#(v1.2) now in units of air concentration 
lnQCC	= 0.0;	# Scaled by BW^0.75 and species-specific central estimates

M_VRespLumC = 1.0; #(v1.2)
M_VRespEffC = 1.0; #(v1.2)
M_lnQCC	= 1.0;
M_lnVMaxC	= 1.0;
M_lnVPRC	= 1.0;
M_lnPRespC = 1.0;
M_VLivC	= 1.0;
M_lnISOx = 0.0; # P450 oxidative metabolism
M_lnPBC = 1.0;

V_VRespLumC = 1.0; #(v1.2)
V_VRespEffC = 1.0; #(v1.2)
V_lnVMaxC	= 1.0;
V_lnQCC	= 1.0;
V_lnVPRC	= 1.0;
V_lnPRespC = 1.0;
V_VLivC	= 1.0;
V_lnISOx = 1.0; # P450 oxidative metabolism
V_lnPBC = 1.0;


## Defaults for input parameters     
QPmeas = 0.0;	# Measured value of Alveolar ventilation QP 

States = {
	AInhResp,
	AResp,
	AExhResp,
	ACh,
};


Inputs = {
Conc
};

Outputs = {
	CInhPPM,
};

Initialize {
	VPR = exp(lnVPRC)* (Species == 3 ? 2.5 : (Species == 2 ? 1.9 : 0.96 ));	
	VRespEfftmp = VRespEffC * BW * (Species == 3 ? 0.0007 : (Species == 2 ? 0.0005 : 0.00018 ));
	PResp = exp(lnPRespC) * (Species == 3 ? 2.6 : (Species == 2 ? 1.0 : 1.3 ));
	PB = (PBmeas > 0.0 ? PBmeas : (exp(lnPBC) * (Species == 3 ? 15. : (Species == 2 ? 22. : 9.5 )))); # Blood-air
	VRespEff = VRespEfftmp * PResp * PB;
	BW75 = pow(BW, 0.75);
	VRespLum = VRespLumC * BW * (Species == 3 ? (0.00014/0.03) : (Species == 2 ? (0.0014/0.3) : (0.167/70) )); 
	VMaxClara = exp(lnVMaxLungLivC) * VMax *
	(Species == 3 ? (1.03/1.87*0.7/5.5):(Species == 2 ? (0.08/0.82*0.5/3.4):(0.03/0.33*(Male == 0 ? (0.42/1.4) : (0.5/1.8)))));
	KMClara = exp(lnKMClara);
	VLiv = VLivC * BW *
	(Species == 3 ? 0.055 : (Species == 2 ? 0.034 : (Male == 0 ? 0.023 : 0.025) ));
	
    VMax = exp(lnISOx) * VLiv*exp(lnVMaxC)* # apply interstrain scaling
    (Species == 3 ? 2700. : (Species == 2 ? 600. : 255.));

# CC data initialization
	Rodents = (CC > 0 ? NRodents : 0.0); # Closed chamber simulation
	VCh = (CC > 0 ? VChC - (Rodents * BW) : 1.0); 
	kLoss = (CC > 0 ? exp(lnkLossC) : 0.0);
	QC = exp(lnQCC) * BW75 * (Species == 3 ? 11.6 : (Species == 2 ? 13.3 : 16.0 ));
	QPsamp = QC*VPR;
	

	ACh = (CC * VCh * MWTCE) / 24450.0;    # Initial amount in chamber
};

Dynamics{
# QP uses QPmeas if value is > 0, otherwise uses sampled value 
	QP = (QPmeas > 0 ? QPmeas : QPsamp);
#Scale diffusion flow rate
	DResp = DResptmp * QP;


# Inhalation route
    QM = QP/0.7;	# Minute-volume
    CInhResp = AInhResp/VRespLum;
    CResp = AResp/VRespEff;
    CExhResp = AExhResp/VRespLum;
    RAMetLng = VMaxClara * CResp/(KMClara + CResp);
    CMixExh = (CExhResp > 0 ? CExhResp : 1e-15);

    CInh = (CC > 0 ? ACh/VCh : Conc*MWTCE/24450.0); # in mg/l
    
    dt(AInhResp) = (QM*CInh + DResp*(CResp-CInhResp) - QM*CInhResp);
    dt(AResp) = (DResp*(CInhResp + CExhResp - 2*CResp) - RAMetLng);  
    #dt(AExhResp) = (QM*(CInhResp-CExhResp) + QP*(CArt_tmp/PB-CInhResp) + DResp*(CResp-CExhResp));
    dt(AExhResp) = (QM*(CInhResp-CExhResp) + DResp*(CResp-CExhResp));
    dt(ACh) = (Rodents * (QM * CMixExh - QM * ACh/VCh)) - (kLoss * ACh);
};

CalcOutputs{
	CInhPPM = (ACh< 1.0e-15 ? 1.0e-15 : ACh/VCh*24450.0/MWTCE); 

};

End.
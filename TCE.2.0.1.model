#-----------------------------------------------------------------------
# 			            Initial Parameters 
#               (plus params for added compartments)
#-----------------------------------------------------------------------


# Chemical Parameters
## Molecular Weights
MWTCE = 131.39;  # TCE


#-----------------
## Flows
QC	= 1;	# Cardiac output (L/hr)
QPsamp	= 1;	# Alveolar ventilation (L/hr)
VPR	= 1;	# Alveolar ventilation-perfusion ratio
QFatCtmp	= 1;	# Scaled fat blood flow
QGutCtmp	= 1;	# Scaled gut blood flow
QLivCtmp	= 1;	# Scaled liver blood flow
QSlwCtmp	= 1;	# Scaled slowly perfused blood flow
DResptmp	= 1;	#(v1.2) Respiratory lumen:tissue diffusive clearance rate (L/hr) [scaled to QP]
QKidCtmp	= 1;	# Scaled kidney blood flow
#FracPlas	= 1;	# Fraction of blood that is plasma (1-hematocrit)

## Volumes
VFat	= 1;	# Fat compartment volume (L)
VGut	= 1;	# Gut compartment volume (L)
VLiv	= 1;	# Liver compartment volume (L)
VRap	= 1;	# Rapidly perfused compartment volume (L)
VRespLum	= 1;	#(v1.2) Volume of respiratory lumen (L air)
VRespEff	= 1;	#(v1.2) Effective volume for respiratory tissue (L air) = V(tissue) * Resp:Air partition coefficient
VKid	= 1;	# Kidney compartment volume (L)
VBld	= 1;	# Blood compartment volume (L)
VSlw	= 1;	# Slowly perfused compartment volume (L)
#VPlas	= 1;	# Plasma compartment volume [fraction of blood] (L)
#VBod	= 1;	# TCA Body compartment volume [not incl. blood+liver] (L)
#VBodTCOH	= 1;	# TCOH/G Body compartment volume [not incl. liver] (L)

## Distribution/partitioning
PB	= 1;	# TCE Blood/air partition coefficient
PFat	= 1;	# TCE Fat/Blood partition coefficient
PGut	= 1;	# TCE Gut/Blood partition coefficient
PLiv	= 1;	# TCE Liver/Blood partition coefficient
PRap	= 1;	# TCE Rapidly perfused/Blood partition coefficient
PResp	= 1;	#(v1.2) TCE Respiratory tissue:air partition coefficient
PKid	= 1;	# TCE Kidney/Blood partition coefficient
PSlw	= 1;	# TCE Slowly perfused/Blood partition coefficient

## Oral absorption
kTSD	= 1.4;	# TCE Stomach-duodenum transfer coefficient (/hr)
kAS	= 1.4;	# TCE Stomach absorption coefficient (/hr)
kTD	= 0.1;	# TCE Duodenum-feces transfer coefficient (/hr)
kAD	= 0.75;	# TCE Duodenum absorption coefficient (/hr)

## TCE Metabolism
VMax1	= 1;	# VMax for hepatic TCE oxidation (mg/hr)
KM1	= 1;	# KM for hepatic TCE oxidation (mg/L)
VMax2	= 1;	# VMax for hepatic TCE GSH conjugation (mg/hr)
KM2	= 1;	# KM for hepatic TCE GSH conjugation (mg/L)
VMaxKid	= 1;	# VMax for renal TCE GSH conjugation (mg/hr)
KMKid	= 1;	# KM for renal TCE GSH conjugation (mg/L)
VMaxClara	= 1;	# VMax for Tracheo-bronchial TCE oxidation (mg/hr)
KMClara	= 1;	# KM for Tracheo-bronchial TCE oxidation (mg/L)

## Closed chamber and other exposure parameters 
Rodents	= 1;	# Number of rodents in closed chamber data
VCh	= 1;	# Chamber volume for closed chamber data
kLoss	= 1;	# Rate constant for closed chamber air loss
CC 	= 0.0;	# Initial chamber concentration (ppm)
TChng 	= 0.003;	# IV infusion duration (hour)

## Flag for species, sex -- these are global parameters
BW	= 0.0;  # Species-specific defaults during initialization
Male 	= 1.0;	# 1 = male, 0 = female
Species	= 1.0;	# 1 = human, 2 = rat, 3 = mouse

## Potentially measured covariates (constants)
PBmeas	= 0.0;	# Measured blood-air partition coefficient
VFatCmeas	= 0.0; # Fractional volume fat
BWmeas	= 0.0;	# Body weight 

#----------------------------
# Global Sampling Parameters 
# Flow Rates 
lnQCC	= 0.0;	# Scaled by BW^0.75 and species-specific central estimates
lnVPRC	= 0.0;	# Scaled to species-specific central estimates

# Fractional Blood Flows to Tissues (fraction of cardiac output)
QFatC	= 1.0;	# Scaled to species-specific central estimates
QGutC	= 1.0;	# Scaled to species-specific central estimates
QLivC	= 1.0;	# Scaled to species-specific central estimates
QSlwC	= 1.0;	# Scaled to species-specific central estimates
#(v1.2) QTBC	= 1.0;	# Scaled to species-specific central estimates
QKidC	= 1.0;	# Scaled to species-specific central estimates
FracPlasC	= 1.0;	# Scaled to species-specific central estimates
lnDRespC	= 0.0;	#(v1.2) Scaled to alveolar ventilation rate in dynamics

# Fractional Tissue Volumes (fraction of BW)
VFatC	= 1.0;	# Scaled to species-specific central estimates
VGutC	= 1.0;	# Scaled to species-specific central estimates
VLivC	= 1.0;	# Scaled to species-specific central estimates
VRapC	= 1.0;	# Scaled to species-specific central estimates
VRespLumC	= 1.0;	# Scaled to species-specific central estimates
VRespEffC	= 1.0;	# Scaled to species-specific central estimates
VKidC	= 1.0;	# Scaled to species-specific central estimates
VBldC	= 1.0;	# Scaled to species-specific central estimate

# Partition Coefficients for TCE
lnPBC	= 0.0;	# Scaled to species-specific central estimates
lnPFatC	= 0.0;	# Scaled to species-specific central estimates
lnPGutC	= 0.0;	# Scaled to species-specific central estimates
lnPLivC	= 0.0;	# Scaled to species-specific central estimates
lnPRapC	= 0.0;	# Scaled to species-specific central estimates
lnPRespC= 0.0;	#(v1.2) Scaled to species-specific central estimates
lnPKidC	= 0.0;	# Scaled to species-specific central estimates
lnPSlwC	= 0.0;	# Scaled to species-specific central estimates

# Oral Absorption rates
lnkTSD	= 0.336;
lnkAS	= 0.336;
lnkTD	= -2.303;
lnkAD	= -0.288;

# TCE Metabolism
lnVMax1C	= 0.0;	# Scaled by liver weight and species-specific central estimates
lnKM1C	= 0.0;	# Scaled to species-specific central estimates
lnCl1C	= 0.0;	# Scaled to species-specific central estimates
lnVMax2C	= 0.0;	# Scaled by liver weight and species-specific central estimates
lnKM2C	= 0.0;	# Scaled to species-specific central estimates
lnCl2C	= 0.0;	# Scaled to species-specific central estimates
lnVMaxKidC = 0.0;
lnClKidC = 0.0;
lnKMKidC = 0.0;
lnKMClara	= 0.0;	#(v1.2) now in units of air concentration 

# Inter-strain scaling - relative to B6C3F1
lnISOx = 0.0; # P450 oxidative metabolism
lnISConj = 0.0; # GSH conjugation metabolisma

# Closed chamber parameters
NRodents	= 1;	# 
VChC	= 1;	# 
lnkLossC	= 0;	# 

## Defaults for input parameters     
QPmeas = 0.0;	# Measured value of Alveolar ventilation QP 

States = {
##-- TCE uptake
	AStom,		# Amount of TCE in stomach
	ADuod,		# oral gavage absorption -- mice and rats only
#(v1e)	AExc,		# excreted in feces from gavage -- mice and rats only
#(v1e)	AO, 		# total absorbed
	InhDose,	# Amount inhaled 
##-- TCE in the body
	ARap,		# Amount in rapidly perfused tissues
	ASlw,		# Amount in slowly perfused tissues
	AFat,		# Amount in fat
	AGut,		# Amount in gut
	ALiv,		# Amount in liver
#(v1j)	ATB, 		# Amount in TB -- candidate to remove and put in Rap
				# comment out for #(v1b)(v1c)
#(v1.2)	RTB,	#(v1j) Rate of metabolism in TB
	AKid, 		# Amount in Kidney -- previously in Rap tissue
	ABld, 		# Amount in Blood -- previously in Rap tissue
				# comment out for #(v1a)(v1c)
	AInhResp,	#(v1.2) Amount in respiratory lumen during inhalation
	AResp,		#(v1.2) Amount in respiratory tissue
	AExhResp,	#(v1.2) Amount in respiratory lumen during exhalation
	
# TCE Mass Balance 
	AMetLng, AMetLiv1, AMetLiv2, AMetKid, AExh,
	ACh	    # Amount in closed chamber -- mice and rats only
	
};


Inputs = {
##-- TCE dosing
	Conc,		# Inhalation exposure conc. (ppm)
	IVDose,		# IV dose (mg/kg)
	PDose,		# Oral gavage dose (mg/kg)
	Drink,		# Drinking water dose (mg/kg/day)
	IADose,		# Inter-arterial (v1f)
	PVDose,		# Portal Vein (v1f)
};

Outputs = {
	CInhPPM, CAlv, CAlvPPM, CVen,CBldMix,
	
	## PARAMETERS #(vrisk3)
  QCnow, # (vrisk3) #Cardiac output (L/hr)
  QP, # (vrisk3) #Alveolar ventilation (L/hr)
  QFatSC, # (vrisk3) #Scaled fat blood flow
  QGutSC, # (vrisk3) #Scaled gut blood flow
  QLivSC, # (vrisk3) #Scaled liver blood flow
  QSlwSC, # (vrisk3) #Scaled slowly perfused blood flow
  QRapSC, # (vrisk3) #Scaled rapidly perfused blood flow
  QKidSC, # (vrisk3) #Scaled kidney blood flow
  DResp, # (vrisk3) #Respiratory lumen:tissue diffusive clearance rate
  VFatSC, # (vrisk3) #Fat fractional compartment volume 
  VGutSC, # (vrisk3) #Gut fractional compartment volume
  VLivSC, # (vrisk3) #Liver fractional compartment volume
  VRapSC, # (vrisk3) #Rapidly perfused fractional compartment volume
  VRespLumSC, # (vrisk3) # Fractional volume of respiratory lumen
  VRespEffSC, # (vrisk3) #Effective fractional volume of respiratory tissue
  VKidSC, # (vrisk3) #Kidney fractional compartment volume
  VBldSC, # (vrisk3) #Blood fractional compartment volume 
  VSlwSC, # (vrisk3) #Slowly perfused fractional compartment volume 
  
};

Initialize {
	BW = (BWmeas > 0.0 ? BWmeas : (Species == 3 ? 0.03 : (Species == 2 ? 0.3 : (Male == 0 ? 60.0 : 70.0) )));
	BW75 = pow(BW, 0.75);
	#BW25 = pow(BW, 0.25);
	QC = exp(lnQCC) * BW75 * 	# Mouse, Rat, Human (default)
		(Species == 3 ? 11.6 : (Species == 2 ? 13.3 : 16.0 ));
	VPR = exp(lnVPRC)* (Species == 3 ? 2.5 : (Species == 2 ? 1.9 : 0.96 ));	
	QPsamp = QC*VPR;
	DResptmp = exp(lnDRespC);
	QFatCtmp = QFatC*
	(Species == 3 ? 0.07 : (Species == 2 ? 0.07 : (Male == 0 ? 0.085 : 0.05) ));
	QGutCtmp = QGutC*
	(Species == 3 ? 0.141 : (Species == 2 ? 0.153 : (Male == 0 ? 0.21 : 0.19) ));
	QLivCtmp = QLivC*
	(Species == 3 ? 0.02 : (Species == 2 ? 0.021 : 0.065 ));
	QSlwCtmp = QSlwC*
	(Species == 3 ? 0.217 : (Species == 2 ? 0.336 : (Male == 0 ? 0.17 : 0.22) ));
	QKidCtmp = QKidC*
		(Species == 3 ? 0.091 : (Species == 2 ? 0.141 : (Male == 0 ? 0.17 : 0.19) ));
	VFat = BW * (VFatCmeas > 0.0 ? VFatCmeas : (VFatC * (Species == 3 ? 0.07 : (Species == 2 ? 0.07 : (Male == 0 ? 0.317 : 0.199)  ))));
        VGut = VGutC * BW *
	(Species == 3 ? 0.049 : (Species == 2 ? 0.032 : (Male == 0 ? 0.022 : 0.020) ));
        VLiv = VLivC * BW *
	(Species == 3 ? 0.055 : (Species == 2 ? 0.034 : (Male == 0 ? 0.023 : 0.025) ));
        VRap = VRapC * BW * ## VRaptmp = VRapC * BW * #(v1a)#(v1b)#(v1c) -- temporary variable
	(Species == 3 ? 0.100 : (Species == 2 ? 0.088 : (Male == 0 ? 0.093 : 0.088) ));
	VRespLum = VRespLumC * BW * 
	(Species == 3 ? (0.00014/0.03) : (Species == 2 ? (0.0014/0.3) : (0.167/70) )); #(v1.2) Lumenal volumes from Styrene model (Sarangapani et al. 2002)
	VRespEfftmp = VRespEffC * BW * 
	(Species == 3 ? 0.0007 : (Species == 2 ? 0.0005 : 0.00018 ));
	VKid = VKidC * BW *
	(Species == 3 ? 0.017 : (Species == 2 ? 0.007 : (Male == 0 ? 0.0046 : 0.0043) ));
        VBld = VBldC * BW *
	(Species == 3 ? 0.049 : (Species == 2 ? 0.074 : (Male == 0 ? 0.068 : 0.077) ));
	VSlw = (Species == 3 ? 0.8897 : (Species == 2 ? 0.8995 : (Male == 0 ? 0.85778 : 0.856))) * BW 
		- VFat - VGut - VLiv - VRap - VRespEfftmp - VKid - VBld;
	
	

	PB = (PBmeas > 0.0 ? PBmeas : (exp(lnPBC) * (Species == 3 ? 15. : (Species == 2 ? 22. : 9.5 )))); # Blood-air
PFat = exp(lnPFatC) *		# Fat/blood
		(Species == 3 ? 36. : (Species == 2 ? 27. : 67. ));
PGut = exp(lnPGutC) * 		# Gut/blood
		(Species == 3 ? 1.9 : (Species == 2 ? 1.4 : 2.6 ));
		 PLiv = exp(lnPLivC) * 		# Liver/blood
		(Species == 3 ? 1.7 : (Species == 2 ? 1.5 : 4.1 ));
	PRap = exp(lnPRapC) * 		# Rapidly perfused/blood
		       ## PRaptmp = exp(lnPRapC) * #(v1a)#(v1b)#(v1c) -- temporary
		(Species == 3 ? 1.9 : (Species == 2 ? 1.3 : 2.6 ));	
	 PResp = exp(lnPRespC) * 		# Resp/blood = 
		(Species == 3 ? 2.6 : (Species == 2 ? 1.0 : 1.3 ));
		
	VRespEff = VRespEfftmp * PResp * PB;
	PKid = exp(lnPKidC) * 		# Slowly perfused/blood
		(Species == 3 ? 2.1 : (Species == 2 ? 1.3 : 1.6 ));
		 PSlw = exp(lnPSlwC) * 		# Slowly perfused/blood
		(Species == 3 ? 2.4 : (Species == 2 ? 0.58 : 2.1 ));
		
	PResp = exp(lnPRespC) * (Species == 3 ? 2.6 : (Species == 2 ? 1.0 : 1.3 ));

	KMClara = exp(lnKMClara);

        VMax1 = exp(lnISOx) * VLiv*exp(lnVMax1C)* # apply interstrain scaling
		(Species == 3 ? 2700. : (Species == 2 ? 600. : 255.));
	
	  KM1 = (Species == 3 ? 36.*exp(lnKM1C) : (Species == 2 ? 21.*exp(lnKM1C) : VMax1/(VLiv*66.*exp(lnCl1C))));
	
	VMax2 = exp(lnISConj) * VLiv*(Species == 3 ? (300.*exp(lnVMax2C)) : (Species == 2 ? (66.*exp(lnVMax2C)) : (2.9*19.*exp(lnCl2C+lnKM2C))));
      KM2 = (Species == 3 ? (VMax2/(VLiv*1.53*exp(lnCl2C))) : (Species == 2 ? (VMax2/(VLiv*0.25*exp(lnCl2C))) : 2.9*exp(lnKM2C)));
	VMaxKid = exp(lnISConj) * VKid*(Species == 3 ? (60.*exp(lnVMaxKidC)) : (Species == 2 ? (6.0*exp(lnVMaxKidC)) : (2.7*230.*exp(lnClKidC+lnKMKidC))));
        KMKid = (Species == 3 ? (VMaxKid/(VKid*0.34*exp(lnClKidC))) : (Species == 2 ? (VMaxKid/(VKid*0.026*exp(lnClKidC))) : 2.7*exp(lnKMKidC)));
    	
	
# CC data initialization
	Rodents = (CC > 0 ? NRodents : 0.0); # Closed chamber simulation
	VCh = (CC > 0 ? VChC - (Rodents * BW) : 1.0); 
	kLoss = (CC > 0 ? exp(lnkLossC) : 0.0);
	QC = exp(lnQCC) * BW75 * (Species == 3 ? 11.6 : (Species == 2 ? 13.3 : 16.0 ));
	QPsamp = QC*VPR;
	

	ACh = (CC * VCh * MWTCE) / 24450.0;    # Initial amount in chamber
};

Dynamics{
# QP uses QPmeas if value is > 0, otherwise uses sampled value 
	QP = (QPmeas > 0 ? QPmeas : QPsamp);
# Scale diffusion flow rate
	DResp = DResptmp * QP;
# QCnow uses QPmeas/VPR if QPmeas > 0, otherwise uses sampled value 
	QCnow = (QPmeas > 0 ? QPmeas/VPR : QC);

# These done here in dynamics in case QCnow changes 
# Blood Flows to Tissues (L/hr)
  QFat = (QFatCtmp) * QCnow; # 
  QGut = (QGutCtmp) * QCnow; # 
  QLiv = (QLivCtmp) * QCnow; # 
  QSlw = (QSlwCtmp) * QCnow; # 
  QKid = (QKidCtmp) * QCnow; # 
  QGutLiv = QGut + QLiv; # 
  QRap = QCnow - QFat - QGut - QLiv - QSlw - QKid; #(v1.2)

  QFatSC = QFat/QCnow; # (vrisk3) #Scaled fat blood flow
  QGutSC = QGut/QCnow; # (vrisk3) #Scaled gut blood flow
  QLivSC = QLiv/QCnow; # (vrisk3) #Scaled liver blood flow
  QSlwSC = QSlw/QCnow; # (vrisk3) #Scaled slowly perfused blood flow
  QRapSC = QRap/QCnow; # (vrisk3) #Scaled rapidly perfused blood flow
  QKidSC = QKid/QCnow; # (vrisk3) #Scaled kidney blood flow


  VFatSC=VFat/BW; # (vrisk3) #Fat fractional compartment volume 
  VGutSC=VGut/BW; # (vrisk3) #Gut fractional compartment volume
  VLivSC=VLiv/BW; # (vrisk3) #Liver fractional compartment volume
  VRapSC=VRap/BW; # (vrisk3) #Rapidly perfused fractional compartment volume
  VRespLumSC=VRespLum/BW; # (vrisk3) # Fractional volume of respiratory lumen
  VRespEffSC=VRespEff/BW; # (vrisk3) #Effective fractional volume of respiratory tissue
  VKidSC=VKid/BW; # (vrisk3) #Kidney fractional compartment volume
  VBldSC=VBld/BW; # (vrisk3) #Blood fractional compartment volume 
  VSlwSC=VSlw/BW; # (vrisk3) #Slowly perfused fractional compartment volume 

#### TCE DOSING
## IV route
    kIV = (IVDose * BW) / TChng;# IV infusion rate (mg/hr)
    kIA = (IADose * BW) / TChng;	# IA infusion rate (mg/hr) (v1f)
    kPV = (PVDose * BW) / TChng;	# PV infusion rate (mg/hr) (v1f)
    kStom = (PDose * BW) / TChng;# PO dose rate (into stomach) (mg/hr) (v1.2.1)

## Oral route
# Amount of TCE in stomach -- for oral dosing only (mg)
#(v1.2.1)    dt(AStom) = PDose * BW - AStom * (kAS + kTSD);
    dt(AStom) = kStom - AStom * (kAS + kTSD);
# Amount of TCE in duodenum -- for oral dosing only (mg)
    dt(ADuod) = (kTSD * AStom) - (kAD + kTD) * ADuod;
# Rate of absorption from drinking water
    kDrink = (Drink * BW) / 24.0; #Ingestion rate via drinking water (mg/hr)
# Total rate of absorption including gavage and drinking water
    RAO = kDrink + (kAS * AStom) + (kAD * ADuod);
## Inhalation route
    CInh = (CC > 0 ? ACh/VCh : Conc*MWTCE/24450.0); # in mg/l

#****Blood (venous)************************************************************
# Tissue Concentrations (mg/L)
	CRap = ARap/VRap; 
	CSlw = ASlw/VSlw; 
	CFat = AFat/VFat; 
	CGut = AGut/VGut; 
	CLiv = ALiv/VLiv; 
	CKid = AKid/VKid; 
# Venous Concentrations (mg/L)
	CVRap = CRap / PRap;
	CVSlw = CSlw / PSlw;
	CVFat = CFat / PFat;  
	CVGut = CGut / PGut;
	CVLiv = CLiv / PLiv;
#(v1.2)	CVTB = CTB / PTB;	# Comment out for #(v1b)#(v1c)
	CVKid = CKid / PKid;
# Concentration of TCE in mixed venous blood (mg/L)
	CVen = ABld/VBld; # Comment out for #(v1a)#(v1c)
		##	CVen = (QFat*CVFat + QGutLiv*CVLiv + QSlw*CVSlw + #(v1a)#(v1c)
		##		QRap*CVRap + QTB*CVTB + QKid*CVKid + kIV) / QCnow; #(v1a)
		##		QRap*CVRap + QKid*CVKid + kIV) / QCnow; #(v1c)
# Dynamics for blood
    dt(ABld) = (QFat*CVFat + QGutLiv*CVLiv + QSlw*CVSlw + # Comment out for #(v1a)#(v1c)
		QRap*CVRap + QKid*CVKid + kIV) - CVen * QCnow; #(v1.2)(v1b)


# Inhalation route
    QM = QP/0.7;	# Minute-volume
    CInhResp = AInhResp/VRespLum;
    CResp = AResp/VRespEff;
    CExhResp = AExhResp/VRespLum;
    dt(AInhResp) = (QM*CInh + DResp*(CResp-CInhResp) - QM*CInhResp);
    RAMetLng = VMaxClara * CResp/(KMClara + CResp);
    dt(AResp) = (DResp*(CInhResp + CExhResp - 2*CResp) - RAMetLng);  
    CArt_tmp = (QCnow*CVen + QP*CInhResp)/(QCnow + (QP/PB));
    dt(AExhResp) = (QM*(CInhResp-CExhResp) + QP*(CArt_tmp/PB-CInhResp) + 
		DResp*(CResp-CExhResp));
    CMixExh = (CExhResp > 0 ? CExhResp : 1e-15); #(v1.2) mixed exhaled breath

# Concentration in alveolar air (mg/L) #(v1f)
	#(v1.2) Correction factor for exhaled air to account for
	# absorption/desorption/metabolism in respiratory tissue
	# = 1 if DResp = 0
	ExhFactor_den = (QP * CArt_tmp / PB + (QM-QP)*CInhResp);
	ExhFactor = (ExhFactor_den > 0) ? (
		QM * CMixExh / ExhFactor_den) : 1;
	#(v1.2) End-exhaled breath (corrected for absorption/
	#	desorption/metabolism in respiratory tissue)
	CAlv = CArt_tmp / PB * ExhFactor;
# Concentration in arterial blood entering circulation (mg/L) #(v1f)
	CArt = CArt_tmp + kIA/QCnow;	# add inter-arterial dose

#****Other dynamics for inhalation/exhalation *********************************
# Dynamics for amount of TCE in closed chamber
#(v1.2)    dt(ACh) = (Rodents * (QP * CAlv - QP * ACh/VCh)) - (kLoss * ACh);
    dt(ACh) = (Rodents * (QM * CMixExh - QM * ACh/VCh)) - (kLoss * ACh);
	# (v1.2)
#**** Non-metabolizing tissues ************************************************
# Amount of TCE in rapidly perfused tissues (mg)
    dt(ARap) = QRap * (CArt - CVRap); ##	- RAMetLng; #(v1b)#(v1c)
# Amount of TCE in slowly perfused tissues
    dt(ASlw) = QSlw * (CArt - CVSlw);
# Amount of TCE in fat tissue (mg)
    dt(AFat) = QFat*(CArt - CVFat);
# Amount of TCE in gut compartment (mg)
    dt(AGut) = (QGut * (CArt - CVGut)) + RAO;

#**** Liver *******************************************************************
# Rate of TCE oxidation by P450 to TCA, TCOH, and other (DCA) in liver (mg/hr)
	RAMetLiv1 = (VMax1 * CVLiv) / (KM1 + CVLiv);
# Rate of TCE metabolized to DCVG in liver (mg)
	RAMetLiv2 = (VMax2 * CVLiv) / (KM2 + CVLiv);
# Dynamics for amount of TCE in liver (mg)
    dt(ALiv) = (QLiv * (CArt - CVLiv)) + (QGut * (CVGut - CVLiv)) 
			- RAMetLiv1 - RAMetLiv2 + kPV; # add PV dose #(v1f)

#**** Kidney ******************************************************************
# Rate of TCE metabolized to DCVG in kidney (mg) # 
	RAMetKid = (VMaxKid * CVKid) / (KMKid + CVKid);
# Amount of TCE in kidney compartment (mg)
    dt(AKid) = (QKid * (CArt - CVKid)) - RAMetKid;
    
    #**** Mass Balance for TCE ****************************************************
# Total intake from inhalation (mg)
	RInhDose = QM * CInh;
    dt(InhDose) = RInhDose;

# Total metabolized
    dt(AMetLng) = RAMetLng; #(vrisk)
    dt(AMetLiv1) = RAMetLiv1; #(vrisk)
    dt(AMetLiv2) = RAMetLiv2; #(vrisk)
    dt(AMetKid) = RAMetKid; #(vrisk)

# Amount exhaled (mg)
#(v1.2)	RAExh = QP * CAlv;
	RAExh = QM * CMixExh;
    dt(AExh) = RAExh;
    
};

CalcOutputs{
	CInhPPM = (ACh< 1.0e-15 ? 1.0e-15 : ACh/VCh*24450.0/MWTCE); 
	CAlvPPM = (CAlv < 1.0e-15 ? 1.0e-15 : CAlv * (24450.0 / MWTCE));
	CVen = (CVen < 1.0e-15 ? 1.0e-15 : CVen);
	CBldMix = (CArt+CVen)/2;
};

End.


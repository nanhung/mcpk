#------------------------------------------------------------------------------
# diffusion_2.model
#
# One-dimensional diffusion; partial differential equations model
# solved using the grid method. Time is the first (implicit) dimension. 
# The second dimension is in x. Neumann boundary conditions impose null
# space gradients at the extremities of the segment.
#
# Copyright (c) 2017 Free Software Foundation, Inc.
#------------------------------------------------------------------------------

States = {C_x[1-100]};

Outputs = {J_x[0-101]};

# Model Parameters
k = 0.1;
V = 1;
delta_x = 0.01; # x = {0, 0.1, ..., 1}

Dynamics { 

  # fluxes
  
  J_x[0]     =  0;

  J_x[1]     = -k * (C_x[2]   - C_x[1])   / (2 * delta_x);

  J_x[2-99]  = -k * (C_x[i+1] - C_x[i-1]) / (2 * delta_x);

  J_x[100]   = -k * (C_x[100] - C_x[99])  / (2 * delta_x);

  J_x[101]   =  0;

  # concentrations
  
  dt(C_x[1-100]) = -(J_x[i+1] - J_x[i-1]) / (2 * delta_x);
  
}

End.











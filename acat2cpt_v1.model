# ==============================================================================
# Two-compartment PBPK model with ACAT-like gut model.
# Linear metabolism
#
# Effective permeability is assumed to be the same for each segment
# of the gi tract
#
# version 1.
#
# Units:
# - time in hours
# - volumes in liters
# - masses of substances in micromoles
# - concentrations of substances in microM
#
# Frederic Bois - Jan 2017
# ==============================================================================

States  = {A_central,      # Quantity in central compartment (micromoles)
           A_periph,       # ~           peripheral compartment
           A_stom_lu,      # ~           stomach  lumen
           A_duod_lu,      # ~           duodenum ~
           A_jeju_lu,      # ~           jejunum  ~
           A_ileum_lu,     # ~           ileum    ~
           A_cecum_lu,     # ~           cecum    ~
           A_colon_lu,     # ~           colon    ~
           A_stom_ep,      # ~           stomach epithelium
           A_duod_ep,
           A_jeju_ep,
           A_ileum_ep,
           A_cecum_ep,
           A_colon_ep,
           A_stom_w,       # ~           stomach wall
           A_duod_w,
           A_jeju_w,
           A_ileum_w,
           A_cecum_w,
           A_colon_w,
           A_absorb_stom,  # ~           absorbed from stomach lumen
           A_absorb_duod,
           A_absorb_jeju,
           A_absorb_ileum,
           A_absorb_cecum,
           A_absorb_colon,
           A_absorb_pv,    # ~           reaching the portal vein
           A_met_central,  # ~           metabolized in central
           A_met_stom,
           A_met_duod,
           A_met_jeju,
           A_met_ileum,
           A_met_cecum,
           A_met_colon,
           A_feces};       # ~           excreted in feces

Outputs = {C_central,       # Concentration in central compartment (microM)
		      lnC_central,
           C_periph,
           C_stom_lu,
           C_duod_lu,
           C_jeju_lu,
           C_ileum_lu,
           C_cecum_lu,
           C_colon_lu,
           C_stom_ep,
           C_duod_ep,
           C_jeju_ep,
           C_ileum_ep,
           C_cecum_ep,
           C_colon_ep,
           C_stom_w,
           C_duod_w,
           C_jeju_w,
           C_ileum_w,
           C_cecum_w,
           C_colon_w,
           A_gi_lu,        # Quantity in g.i. tract lumen
           A_gi_ep,        # ~                      epithelium
           A_gi_w,         # ~                      wall
           A_absorb,       # ~ absorbed by the g.i. tract
           A_elim,         # ~ metabolized or excreted
           A_elim_gi,      # ~ metabolized or excreted from the g.i. tract only
           A_organ,        # ~ in the body (including lumina)
           A_total,        # ~ in the body and metabolized or excreted
           Instant_Ka_gi}; # Instantaenous net absorption ratio by g.i. tract

Inputs  = {Oral_dose_rate};# in micromoles / hours


# ------------------------
# Physiological parameters
# ------------------------

# Body mass (kg)
BDM = 70;

# Total blood flow scaling coefficient to body mass
sc_F_total = 15;

# Fractions of total blood flow going to tissues (ref Perdaems 2010)
f_Flow_stom      = 0.024;
f_Flow_duod      = 0.016;
f_Flow_jeju      = 0.056;
f_Flow_ileum     = 0.033;
f_Flow_cecum     = 0.006;
f_Flow_colon     = 0.038;

# Tissue fractions of body mass (ref Perdaems 2010)
f_BDM_stom  = 0.0021;
f_BDM_duod  = 0.0003;
f_BDM_jeju  = 0.0009;
f_BDM_ileum = 0.0006;
f_BDM_cecum = 0.0005;
f_BDM_colon = 0.0048;

# Lumina as fractions of body mass (ref Perdaems 2010)
f_BDM_stom_lu  = 0.0036;
f_BDM_duod_lu  = 0.0003;
f_BDM_jeju_lu  = 0.0023;
f_BDM_ileum_lu = 0.0032;
f_BDM_cecum_lu = 0.0001;
f_BDM_colon_lu = 0.0051;

# Lengths of gi tract segments (ref Perdaems 2010) (decimeters, dm)
Length_stom  = 2.5;
Length_duod  = 2.5;
Length_jeju  = 20;
Length_ileum = 30;
Length_cecum = 1.7;
Length_colon = 12.5;

# Radii of gi tract segments (ref Perdaems 2010) (dm)
Radius_stom  = 0.85;
Radius_duod  = 0.2;
Radius_jeju  = 0.18;
Radius_ileum = 0.15;
Radius_cecum = 0.35;
Radius_colon = 0.25;

# Transit half-lives in lumina (ref Perdaems 2010) (hours)
T12_stom_lu  = 0.25;
T12_duod_lu  = 0.25;
T12_jeju_lu  = 1.02;
T12_ileum_lu = 2.04;
T12_cecum_lu = 4.55;
T12_colon_lu = 13.5;

# pH of luminal contents (ref Perdaems 2010) (unused for now)
PH_stom  = 1.7;
PH_duod  = 6;
PH_jeju  = 6.5;
PH_ileum = 7.4;
PH_cecum = 5.9;
PH_colon = 7;

# Microsomal proteins (mg / g of tissue)
MicroProt_central  = 45;
MicroProt_stom   = 0;     # to check
MicroProt_duod   = 9.45;
MicroProt_jeju   = 8.82;
MicroProt_ileum  = 1.62;
MicroProt_cecum  = 0.0;
MicroProt_colon  = 0.0;

# G.i. tract epithelial thickness (dm)
H_ep = 5.25E-4;


#-------------------------------------
# Substance-specific parameters
#-------------------------------------

# Molecular mass (g/mol)
MM;

# Administration lagtime, if needed
Tlag;

# Absorption on(1)/off(0) switches
f_Abs_stom;
f_Abs_duod;
f_Abs_jeju;
f_Abs_ileum;
f_Abs_cecum;
f_Abs_colon;

# Effective permeability of g.i. tract epithelia
Peff = 1; lnPeff = log(1.0);

# Tissue / blood partition coefficients
# PC_periph  = 1; # No use 
# PC_central = 1; # No use
PC_stom    = 1; lnPC_stom = log(1.0);
PC_duod    = 1; lnPC_duod = log(1.0);
PC_jeju    = 1; lnPC_jeju = log(1.0);
PC_ileum   = 1; lnPC_ileum = log(1.0);
PC_cecum   = 1; lnPC_cecum = log(1.0);
PC_colon   = 1; lnPC_colon = log(1.0);

# Fraction unbound in compartments
Fu_stom     = 1.0; lnFu_stom = log(1.0);
Fu_duod     = 1.0; lnFu_duod = log(1.0);
Fu_jeju     = 1.0; lnFu_jeju = log(1.0);
Fu_ileum    = 1.0; lnFu_ileum = log(1.0);
Fu_cecum    = 1.0; lnFu_cecum = log(1.0);
Fu_colon    = 1.0; lnFu_colon = log(1.0);

# Volumes (L)
V_central = 1.0; lnV_central = log(1.0);
V_periph  = 1.0; lnV_periph = log(1.0);

# Gut Michaelis-Menten Vmax (micromol/min/mg microsomal proteins)
Vmax_vitro = 1.0; lnVmax_vitro = log(1.0);

# Gut Michaelis-Menten Km (microM)
Km_vitro = 1.0; lnKm_vitro = log(1.0);

# Elimination rate constant from central (1/h)
Kmet = 1.0; lnKmet = log(1.0);

# Transfer rate constants between central and peripheral compartments (1/h)
Kc2p = 1; lnKc2p = log(1.0);
Kp2c = 1; lnKp2c = log(1.0);


#-------------------------------------------
# Scaled physiological G.I. tract parameters
#-------------------------------------------

# Blood flows
F_total;

# Volumes (L)
V_stom;
V_duod;
V_jeju;
V_ileum;
V_cecum;
V_colon;

# Tissue volumes in gi tract
V_stom_w;
V_duod_w;
V_jeju_w;
V_ileum_w;
V_cecum_w;
V_colon_w;

# Luminal volumes in gi tract
V_stom_lu;
V_colon_lu;
V_duod_lu;
V_jeju_lu;
V_ileum_lu;
V_cecum_lu;

# Epithelial volumes in gi tract
V_stom_ep;
V_duod_ep;
V_jeju_ep;
V_ileum_ep;
V_cecum_ep;
V_colon_ep;

# Epithelial surface area in gi tract (dm^2)
SA_stom;
SA_duod;
SA_jeju;
SA_ileum;
SA_cecum;
SA_colon;

# Blood flows
Flow_portvein;
Flow_stom;
Flow_duod;
Flow_jeju;
Flow_ileum;
Flow_cecum;
Flow_colon;

# GI tract absorption flows
Ka_stom;
Ka_duod;
Ka_jeju;
Ka_ileum;
Ka_cecum;
Ka_colon;

# In vivo central and gi tract Vmax
Vmax_vivo_stom;
Vmax_vivo_duod;
Vmax_vivo_jeju;
Vmax_vivo_ileum;
Vmax_vivo_cecum;
Vmax_vivo_colon;

# In vivo Km
Km_vivo;

#------------------------
# Population Mean (M_...)
#------------------------
M_lnPeff;
M_lnPC_stom;
M_lnPC_duod; #(3)
M_lnPC_jeju;
M_lnPC_ileum;
M_lnPC_cecum;
M_lnPC_colon;
M_lnFu_stom;
M_lnFu_duod; #(1)
M_lnFu_jeju;
M_lnFu_ileum;
M_lnFu_cecum;
M_lnFu_colon; #(5)
M_lnV_central; #(7)
M_lnV_periph;
M_lnVmax_vitro; #(2)
M_lnKm_vitro; #(4)
M_lnKc2p; #(6)
M_lnKp2c;
#----------------------------
# Population Variance (V_...)
#-----------------------------
V_lnPeff;
V_lnPC_stom;
V_lnPC_duod; #(3)
V_lnPC_jeju;
V_lnPC_ileum;
V_lnPC_cecum;
V_lnPC_colon;
V_lnFu_stom;
V_lnFu_duod; #(1)
V_lnFu_jeju;
V_lnFu_ileum;
V_lnFu_cecum;
V_lnFu_colon; #(5)
V_lnV_central; #(7)
V_lnV_periph;
V_lnVmax_vitro; #(2)
V_lnKm_vitro; #(4)
V_lnKc2p; #(6)
V_lnKp2c;

#-------------------------
#  Data Error (Verr_...)
#-------------------------

Ve_lnC_central;

Initialize {

Peff = exp(lnPeff);
PC_stom = exp(lnPC_stom);
PC_duod = exp(lnPC_duod); #(3)
PC_jeju = exp(lnPC_jeju);
PC_ileum = exp(lnPC_ileum);
PC_cecum = exp(lnPC_cecum);
PC_colon = exp(lnPC_colon);
Fu_stom = exp(lnFu_stom);
Fu_duod = exp(lnFu_duod); #(1)
Fu_jeju = exp(lnFu_jeju);
Fu_ileum= exp(lnFu_ileum);
Fu_cecum = exp(lnFu_cecum);
Fu_colon = exp(lnFu_colon); #(5)
V_central = exp(lnV_central); #(7)
V_periph = exp(lnV_periph);
Vmax_vitro = exp(lnVmax_vitro); #(2)
Km_vitro = exp(lnKm_vitro); #(4)
Kc2p = exp(lnKc2p); #(6)
Kp2c = exp(lnKp2c);	
		
  # Total blood flow
  F_total = sc_F_total * pow(BDM, 0.75);

  # Volumes
  V_stom_w   = BDM * f_BDM_stom;
  V_duod_w   = BDM * f_BDM_duod;
  V_jeju_w   = BDM * f_BDM_jeju;
  V_ileum_w  = BDM * f_BDM_ileum;
  V_cecum_w  = BDM * f_BDM_cecum;
  V_colon_w  = BDM * f_BDM_colon;

  # Blood flows
  Flow_stom     = F_total * f_Flow_stom;
  Flow_duod     = F_total * f_Flow_duod;
  Flow_jeju     = F_total * f_Flow_jeju;
  Flow_ileum    = F_total * f_Flow_ileum;
  Flow_cecum    = F_total * f_Flow_cecum;
  Flow_colon    = F_total * f_Flow_colon;
  Flow_portvein = Flow_stom  + Flow_duod  + Flow_jeju +
                  Flow_ileum + Flow_cecum + Flow_colon;

  # Epithelial surface areas
  SA_stom   = Length_stom  * 2 * 3.1416 * Radius_stom;
  SA_duod   = Length_duod  * 2 * 3.1416 * Radius_duod;
  SA_jeju   = Length_jeju  * 2 * 3.1416 * Radius_jeju;
  SA_ileum  = Length_ileum * 2 * 3.1416 * Radius_ileum;
  SA_cecum  = Length_cecum * 2 * 3.1416 * Radius_cecum;
  SA_colon  = Length_colon * 2 * 3.1416 * Radius_colon;

  # Luminal volumes
  V_stom_lu  = f_BDM_stom_lu  * BDM;
  V_duod_lu  = f_BDM_duod_lu  * BDM;
  V_jeju_lu  = f_BDM_jeju_lu  * BDM;
  V_ileum_lu = f_BDM_ileum_lu * BDM;
  V_cecum_lu = f_BDM_cecum_lu * BDM;
  V_colon_lu = f_BDM_colon_lu * BDM;

  # Epithelial volumes
  V_stom_ep     = H_ep * SA_stom;
  V_duod_ep     = H_ep * SA_duod;
  V_jeju_ep     = H_ep * SA_jeju;
  V_ileum_ep    = H_ep * SA_ileum;
  V_cecum_ep    = H_ep * SA_cecum;
  V_colon_ep    = H_ep * SA_colon;

  # Metabolic parameters, scaled from in vitro values
  Vmax_vivo_stom  = Vmax_vitro * 60 * MicroProt_stom  * V_stom_ep  * 1000;
  Vmax_vivo_duod  = Vmax_vitro * 60 * MicroProt_duod  * V_duod_ep  * 1000;
  Vmax_vivo_jeju  = Vmax_vitro * 60 * MicroProt_jeju  * V_jeju_ep  * 1000;
  Vmax_vivo_ileum = Vmax_vitro * 60 * MicroProt_ileum * V_ileum_ep * 1000;
  Vmax_vivo_cecum = Vmax_vitro * 60 * MicroProt_cecum * V_cecum_ep * 1000;
  Vmax_vivo_colon = Vmax_vitro * 60 * MicroProt_colon * V_colon_ep * 1000;

  Km_vivo = Km_vitro;

  # GI tract absorption flows
  Ka_stom  = Peff * SA_stom  * f_Abs_stom;
  Ka_duod  = Peff * SA_duod  * f_Abs_duod;
  Ka_jeju  = Peff * SA_jeju  * f_Abs_jeju;
  Ka_ileum = Peff * SA_ileum * f_Abs_ileum;
  Ka_cecum = Peff * SA_cecum * f_Abs_cecum;
  Ka_colon = Peff * SA_colon * f_Abs_colon;

} # End of model scaling


Dynamics {

  # Concentrations in compartments
  C_central = A_central / V_central;
  C_periph  = A_periph  / V_periph;

  C_stom_w  = A_stom_w  / V_stom_w;
  C_duod_w  = A_duod_w  / V_duod_w;
  C_jeju_w  = A_jeju_w  / V_jeju_w;
  C_ileum_w = A_ileum_w / V_ileum_w;
  C_cecum_w = A_cecum_w / V_cecum_w;
  C_colon_w = A_colon_w / V_colon_w;

  # Blood concentration at compartment exit
  Ctb_stom_w  = C_stom_w  / PC_stom;
  Ctb_duod_w  = C_duod_w  / PC_duod;
  Ctb_jeju_w  = C_jeju_w  / PC_jeju;
  Ctb_ileum_w = C_ileum_w / PC_ileum;
  Ctb_cecum_w = C_cecum_w / PC_cecum;
  Ctb_colon_w = C_colon_w / PC_colon;

  # Concentrations in lumina
  C_stom_lu  = A_stom_lu  / V_stom_lu;
  C_duod_lu  = A_duod_lu  / V_duod_lu;
  C_jeju_lu  = A_jeju_lu  / V_jeju_lu;
  C_ileum_lu = A_ileum_lu / V_ileum_lu;
  C_cecum_lu = A_cecum_lu / V_cecum_lu;
  C_colon_lu = A_colon_lu / V_colon_lu;

  # Concentrations in epithelia
  C_stom_ep  = A_stom_ep  / V_stom_ep;
  C_duod_ep  = A_duod_ep  / V_duod_ep;
  C_jeju_ep  = A_jeju_ep  / V_jeju_ep;
  C_ileum_ep = A_ileum_ep / V_ileum_ep;
  C_cecum_ep = A_cecum_ep / V_cecum_ep;
  C_colon_ep = A_colon_ep / V_colon_ep;

  # Transfers from lumen to lumen or feces (intestinal transit)
  Rate_stl2dul = (log(2.0) * V_stom_lu  / T12_stom_lu)  * C_stom_lu;
  Rate_dul2jel = (log(2.0) * V_duod_lu  / T12_duod_lu)  * C_duod_lu;
  Rate_jel2ill = (log(2.0) * V_jeju_lu  / T12_jeju_lu)  * C_jeju_lu;
  Rate_ill2cel = (log(2.0) * V_ileum_lu / T12_ileum_lu) * C_ileum_lu;
  Rate_cel2col = (log(2.0) * V_cecum_lu / T12_cecum_lu) * C_cecum_lu;
  Rate_col2fel = (log(2.0) * V_colon_lu / T12_colon_lu) * C_colon_lu;

  # Transfers from lumina to epithelia
  Rate_stl2ste = Ka_stom  * (C_stom_lu  - C_stom_ep);
  Rate_dul2due = Ka_duod  * (C_duod_lu  - C_duod_ep);
  Rate_jel2jee = Ka_jeju  * (C_jeju_lu  - C_jeju_ep);
  Rate_ill2ile = Ka_ileum * (C_ileum_lu - C_ileum_ep);
  Rate_cel2cee = Ka_cecum * (C_cecum_lu - C_cecum_ep);
  Rate_col2coe = Ka_colon * (C_colon_lu - C_colon_ep);

  # Transfers from epithelia to tissues
  Rate_ste2stw = Ka_stom  * (C_stom_ep  - C_stom_w);
  Rate_due2duw = Ka_duod  * (C_duod_ep  - C_duod_w);
  Rate_jee2jew = Ka_jeju  * (C_jeju_ep  - C_jeju_w);
  Rate_ile2ilw = Ka_ileum * (C_ileum_ep - C_ileum_w);
  Rate_cee2cew = Ka_cecum * (C_cecum_ep - C_cecum_w);
  Rate_coe2cow = Ka_colon * (C_colon_ep - C_colon_w);

  # Transfers from tissues to portal vein
  Rate_stw2pv = Flow_stom  * Ctb_stom_w;
  Rate_duw2pv = Flow_duod  * Ctb_duod_w;
  Rate_jew2pv = Flow_jeju  * Ctb_jeju_w;
  Rate_ilw2pv = Flow_ileum * Ctb_ileum_w;
  Rate_cew2pv = Flow_cecum * Ctb_cecum_w;
  Rate_cow2pv = Flow_colon * Ctb_colon_w;

  # Portal rate in
  RateIn_portvein = Rate_stw2pv + Rate_duw2pv + Rate_jew2pv +
                    Rate_ilw2pv + Rate_cew2pv + Rate_cow2pv;

  # Differential equations

  # Quantities absorbed
  dt (A_absorb_stom)  = Rate_stl2ste;
  dt (A_absorb_duod)  = Rate_dul2due;
  dt (A_absorb_jeju)  = Rate_jel2jee;
  dt (A_absorb_ileum) = Rate_ill2ile;
  dt (A_absorb_cecum) = Rate_cel2cee;
  dt (A_absorb_colon) = Rate_col2coe;

  # Quantity reaching the portal vein
  dt (A_absorb_pv) = RateIn_portvein;

  # Elimination and metabolism

  dt (A_met_central) = Kmet * A_central;

  dt (A_met_stom)  = Vmax_vivo_stom * C_stom_ep   /
                     (Km_vivo / Fu_stom + C_stom_ep);

  dt (A_met_duod)  = Vmax_vivo_duod * C_duod_ep   /
                     (Km_vivo / Fu_duod + C_duod_ep);

  dt (A_met_jeju)  = Vmax_vivo_jeju * C_jeju_ep   /
                     (Km_vivo / Fu_jeju + C_jeju_ep);

  dt (A_met_ileum) = Vmax_vivo_ileum * C_ileum_ep /
                     (Km_vivo / Fu_ileum + C_ileum_ep);

  dt (A_met_cecum) = Vmax_vivo_cecum * C_cecum_ep /
                     (Km_vivo / Fu_cecum + C_cecum_ep);

  dt (A_met_colon) = Vmax_vivo_colon * C_colon_ep /
                     (Km_vivo / Fu_colon + C_colon_ep);

  dt (A_feces) = Rate_col2fel;

  # Distributions

  dt(A_periph)  = Kc2p * A_central - Kp2c * A_periph;
  dt(A_central) = RateIn_portvein - Flow_portvein * C_central -
                  dt(A_periph) - dt(A_met_central) ;


  dt(A_stom_lu) = Oral_dose_rate - Rate_stl2dul - Rate_stl2ste;
  dt(A_stom_ep) = Rate_stl2ste - Rate_ste2stw - dt (A_met_stom);
  dt(A_stom_w)  = Flow_stom * C_central + Rate_ste2stw - Rate_stw2pv;

  dt(A_duod_lu) = Rate_stl2dul - Rate_dul2jel - Rate_dul2due;
  dt(A_duod_ep) = Rate_dul2due - Rate_due2duw - dt (A_met_duod);
  dt(A_duod_w)  = Flow_duod * C_central + Rate_due2duw - Rate_duw2pv;

  dt(A_jeju_lu) = Rate_dul2jel - Rate_jel2ill - Rate_jel2jee;
  dt(A_jeju_ep) = Rate_jel2jee - Rate_jee2jew - dt (A_met_jeju);
  dt(A_jeju_w)  = Flow_jeju * C_central + Rate_jee2jew - Rate_jew2pv;

  dt(A_ileum_lu) = Rate_jel2ill - Rate_ill2cel - Rate_ill2ile;
  dt(A_ileum_ep) = Rate_ill2ile - Rate_ile2ilw - dt (A_met_ileum);
  dt(A_ileum_w)  = Flow_ileum * C_central + Rate_ile2ilw - Rate_ilw2pv;

  dt(A_cecum_lu) = Rate_ill2cel - Rate_cel2col - Rate_cel2cee;
  dt(A_cecum_ep) = Rate_cel2cee - Rate_cee2cew - dt (A_met_cecum);
  dt(A_cecum_w)  = Flow_cecum * C_central + Rate_cee2cew - Rate_cew2pv;

  dt(A_colon_lu) = Rate_cel2col - Rate_col2coe - dt (A_feces);
  dt(A_colon_ep) = Rate_col2coe - Rate_coe2cow - dt (A_met_colon);
  dt(A_colon_w)  = Flow_colon * C_central + Rate_coe2cow - Rate_cow2pv;

}

CalcOutputs {

  # Mass balance checking

  A_gi_lu   = A_stom_lu  + A_duod_lu + A_jeju_lu + A_ileum_lu +
              A_cecum_lu + A_colon_lu;

  A_gi_ep   = A_stom_ep  + A_duod_ep + A_jeju_ep + A_ileum_ep +
              A_cecum_ep + A_colon_ep;

  A_gi_w    = A_stom_w  + A_duod_w + A_jeju_w + A_ileum_w +
              A_cecum_w + A_colon_w;

  A_absorb  = A_absorb_stom  + A_absorb_duod  +
              A_absorb_jeju  + A_absorb_ileum +
              A_absorb_cecum + A_absorb_colon;

  A_elim_gi = A_met_stom  + A_met_duod  + A_met_jeju  +
              A_met_ileum + A_met_cecum + A_met_colon + A_feces;

  A_elim    = A_met_central + A_elim_gi;

  A_organ   = A_central + A_periph + A_gi_lu + A_gi_ep + A_gi_w;

  A_total   = A_organ + A_elim;

  Instant_Ka_gi = (A_gi_lu > 0 ? A_absorb_pv / A_gi_lu : 0);
  
  lnC_central = (C_central > 0 ? log (C_central) : -20);
  
}

End.

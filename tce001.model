States = {
    AOther,
	ARap,		# Amount in rapidly perfused tissues
	ASlw,		# Amount in slowly perfused tissues
	ABld, 		# Amount in Blood
	AFat,       # Amount in fat
	AGut,		# Amount in gut
	ALiv,		# Amount in liver
	AKid, 		# Amount in Kidney
	ABrn,		# Amount in brain
	ALng        # Amount in Lung
};	

Inputs  = {Oral_input}; 

Outputs = {
    CRap,
    CSlw,
    CBld,
    CFat,
    CGut,
    CLiv,
    CKid,
    CBrn,
    CLng,
    ATotal
};

BW	= 0.0032;  # Species-specific defaults during initialization
BW75 = 0;

# Oral input modeling

AbsDose;                       # Absorbed dose (mg)
Fgutabs = 1;                   #
IngDose    = 1.0;              # ingested input (mg/kg)
Period     = 48.0;             # period of the exposure/no exposure cycle (h)
Tlag       = 0.0;              # Absorption lagtime (h)
Ka = 1;         # Intestinal absortion rate constant (1/h)
Oral_input = PerExp (AbsDose, Period, Tlag, Ka);

QC = 1; QCC = 11.6; 

# Fractional volumes (fraction of body weight).
VGutC = 0.022; 
VLivC = 0.053; 
VKidC = 0.016; 
VBrnC = 0.013; 
VLngC = 0.0061;
VFatC = 0.07;
VRapC = 0.1; 
VBldC = 0.049;
VSlwC = 0.6709;

# Fractional blood flows (fraction of cardiac output).
#http://www.scielo.cl/scielo.php?pid=s0716-97602007000200004&script=sci_arttext
QGutC = 0.141;
QLivC = 0.02;
QKidC = 0.091;
QBrnC = 0.030;
QLngC = 0.312;
QFatC = 0.07;
QSlwC = 0.217;
QRapC = 0.119;

QGut = 1;
QLiv = 1;
QKid = 1;
QBrn = 1;
QLng = 1;
QFat = 1;
QSlw = 1;
QRap = 1;

PGut = 0.1;
PLiv = 0.1;
PKid = 0.1;
PBrn = 0.1;
PLng = 0.1;
PFat = 0.1;
PSlw = 0.1;
PRap = 0.1;

VMax = 1;	# VMax for hepatic TCE oxidation (mg/hr)
KM	= 1;	# KM for hepatic TCE oxidation (mg/L)
VMaxDCVG	= 1;	# VMax for hepatic TCE GSH conjugation (mg/hr)
KMDCVG	= 1;	# KM for hepatic TCE GSH conjugation (mg/L)
VMaxKidDCVG	= 1;	# VMax for renal TCE GSH conjugation (mg/hr)
KMKidDCVG	= 1;	# KM for renal TCE GSH conjugation (mg/L)
VMaxClara	= 1;	# VMax for Tracheo-bronchial TCE oxidation (mg/hr)
KMClara	= 1;	# KM for Tracheo-bronchial TCE oxidation (mg/L)
kUrn = 1;

Initialize {
	AbsDose = IngDose * Fgutabs * BW;
    BW75 = pow(BW, 0.75);
    # Cardiac Output and alveolar ventilation (L/hr) 
	QC = QCC * BW75;
	QGut = QGutC * QC;
	QLiv = QLivC * QC;  
	QKid = QKidC * QC;  
	QBrn = QBrnC * QC; 
	QFat = QFatC * QC;
	QLng = QLngC * QC; 
	QSlw = QSlwC * QC; 
	QRap = QC - QGut - QLiv - QSlw - QKid - QBrn - QFat - QLng; #(v1.2)
}

Dynamics {

#****Blood (venous)************************************************************
	AVRap = ARap / PRap;
	AVSlw = ASlw / PSlw;
	AVGut = AGut / PGut;
	AVLiv = ALiv / PLiv;
	AVKid = AKid / PKid;
	AVBrn = ABrn / PBrn;
	AVFat = AFat / PFat;
    AVLng = ALng / PLng;
# Dynamics for blood
    dt(ABld) = (QGut*AVGut + QFat*AVFat + QBrn*AVBrn + QSlw*AVSlw + QRap*AVRap +
    			 + QLng*AVLng + QLiv*AVLiv + QKid*AVKid ) - ABld * QC;
		
#**** Non-metabolizing tissues ************************************************
# Amount of TCE in rapidly perfused tissues (mg)
    dt(ARap) = QRap * (ABld - AVRap);
# Amount of TCE in slowly perfused tissues
    dt(ASlw) = QSlw * (ABld - AVSlw);
# Amount of TCE in brain tissue (mg)
    dt(ABrn) = QBrn * (ABld - AVBrn);
# Amount of TCE in fat tissue (mg)
    dt(AFat) = QFat * (ABld - AVFat);
# Amount of TCE in gut compartment (mg)
    dt(AGut) = Ka * Oral_input + (QGut * (ABld - AVGut)); # Ka need to put here!

#**** Liver *******************************************************************
# Rate of TCE oxidation by P450 to TCA, TCOH, and other (DCA) in liver (mg/hr)
	RAMetLiv1 = (VMax * AVLiv) / (KM + AVLiv);
# Rate of TCE metabolized to DCVG in liver (mg)
	RAMetLiv2 = (VMaxDCVG * AVLiv) / (KMDCVG + AVLiv);
# Dynamics for amount of TCE in liver (mg)
    dt(ALiv) = (QLiv * (ABld - AVLiv)) - RAMetLiv1 - RAMetLiv2;

#**** Kidney ******************************************************************
# Rate of TCE metabolized to DCVG in kidney (mg) # 
	RAMetKid = (VMaxKidDCVG * AVKid) / (KMKidDCVG + AVKid);
# Rate of urinary excretion of TCE
	RUrn = kUrn * AKid;
# Amount of TCE in kidney compartment (mg)
    dt(AKid) = (QKid * (ABld - AVKid)) - RAMetKid - RUrn;

#**** Lung ******************************************************************
# Rate of TCE oxidation in lung (mg) # 
    RAMetLng = VMaxClara * AVLng/(KMClara + AVLng);
# Dynamics for amount of TCE in lung (mg)    
    dt(ALng) = (QLng * (ABld - AVLng)) - RAMetLng;  
# other    
	dt(AOther) = RAMetLng + RAMetLiv1 + RAMetLiv2 + RAMetKid + RUrn; 
}

CalcOutputs {
# Tissue Concentrations (mg/L)
	CRap = ARap/VRapC/BW; 
	CSlw = ASlw/VSlwC/BW; 
	CFat = AFat/VFatC/BW; 
	CGut = AGut/VGutC/BW; 
	CLiv = ALiv/VLivC/BW; 
	CKid = AKid/VKidC/BW; 
    CBrn = ABrn/VBrnC/BW;
    CLng = ALng/VLngC/BW;
# Concentration of TCE in mixed venous blood (mg/L)
    CBld = ABld/VBldC/BW;
    ATotal = AGut + ABld + AFat + ABrn + ARap + ASlw + ALng + ALiv + AKid + AOther;
}

End.


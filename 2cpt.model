# ==============================================================================
# Two-compartment model with first-order absorption rate and 
# linear elimination (metabolism or other route of elimination) 
#
# version 2
#
# Units: 
# - time in hours
# - volumes in liters
# - masses of substances in micromoles
# - concentrations of substances in microM
#
# version 1 - Frederic Bois - Dec 2016
# version 2 - Nan-Hung Hsieh - Feb 2018
# Updated:
# - Add IV and delay oral doses
# ==============================================================================

States  = {Q_central,       # Quantity in central compartment (micromoles)
           Q_periph,        # ~           peripheral compartment
           Q_release,       # ~        to release
           Q_stom,          # ~        in stomach (micromoles)
           Q_elim};         # ~        eliminated

Outputs = {C_central,       # Concentration in central compartment (microM)
           Q_total,         # Total quantity for mass balance
           ln_C_central};        

Inputs  = {PO_dose,  # immediate dose, in micromoles / h 
           IV_dose_rate};

# Default parameter values
# ========================

# Dosage forms: 0/1 switches, mutually exclusive,
# only one on them should be set to 1
G_immediate = 1; # immediate release (default)
G_delayed = 0;

# 
PO_dose = 1.0; # ingested input (mg)

Period = 24.0; # period of the exposure/no exposure cycle (h)

TChng = 0.01;

# Rate constant (/h)
Kr;  # Release rate
Ka;  # Absortion rate
Ke; # Elimination rate 

lnKr = log(1.0);  
lnKa = log(1.0);  
lnKe = log(0.05);  

# Volumes (L)
# https://sepia.unil.ch/pharmacology/index.php?id=90
V_central;
lnV_central = log(0.3);

# Transfer rate constants between compartments (1/h)
Kc2p;
Kp2c;

lnKc2p = log(11);
lnKp2c = log(5);

# Statistical parameters
# ========================
# Variances inter individual
Vr_Kr            = 0.182; # factor 1.2
Vr_Ka            = 0.182; # factor 1.2
Vr_Ke            = 0.182; # factor 1.2
Vr_V_central     = 0.182; # factor 1.2
Vr_Kc2p          = 0.182; # factor 1.2 
Vr_Kp2c          = 0.182; # factor 1.2

# Variances intra individual
Va_Kr            = 0.182; # factor 1.2
Va_Ka            = 0.182; # factor 1.2
Va_Ke            = 0.182; # factor 1.2  
Va_V_central     = 0.182; # factor 1.2
Va_Kc2p          = 0.182; # factor 1.2
Va_Kp2c          = 0.182; # factor 1.2

#  Data Error (Ve_...)
# ========================

Ve_C_central = 1.3;


Initialize {
Kr = exp(lnKr);
Ka = exp(lnKa);
V_central = exp(lnV_central);
Kc2p = exp(lnKc2p);
Kp2c = exp(lnKp2c);
Ke = exp(lnKe);

} # End of model scaling

Dynamics {

    PO_dose_rate = PO_dose / TChng; # PO dose rate (into stomach) (mg/hr)  
   
  dt (Q_release) = PO_dose_rate - Kr * Q_release;
  dt (Q_stom) = (G_immediate > 0.5 ? PO_dose_rate : (G_delayed > 0.5 ? Kr * Q_release : 0.0)) - Ka * Q_stom;
  dt (Q_elim) = Ke * Q_central;
  dt (Q_periph)  = Kc2p * Q_central - Kp2c * Q_periph;
  dt (Q_central) = IV_dose_rate +  Ka * Q_stom - 
                     dt(Q_elim) - dt(Q_periph);
}

CalcOutputs {
  C_central = (Q_central <= 0 ? 10E-30 : Q_central / V_central);
  Q_total   = Q_central + Q_periph + Q_elim; # check mass balance
  ln_C_central = log(C_central);
}

End.